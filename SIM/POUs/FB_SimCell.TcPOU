<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_SimCell" Id="{04fe6bf7-e7cc-4102-8854-b7f2cb31d082}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SimCell
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	bPartPresentAtInfeed		AT %Q* : BOOL;
	bPartPresentAtOutfeed		AT %Q* : BOOL;
	
	fbInfeedActuator			: FB_SimActuator;
	fbOutfeedActuator			: FB_SimActuator;
	fbInfeedConveyor			: FB_SimAxis;
	fbOutfeedConveyor			: FB_SimAxis;
	fbIndexConveyor				: FB_SimAxis;
	
	fbSimScanner				: FB_SimBarcodeScanner;
	fbSimFiller					: FB_SimFiller;
	fbSimCapper					: FB_SimCapper;
	fbSimWeigher				: FB_SimCheckWeigher;
	
	fbTmr						: ARRAY[0..9] OF TON;
	rand						: ARRAY[0..9] OF DRAND;
	i,j,k						: INT;
	bStationLoaded				: ARRAY[0..3] OF BOOL;
	
	iIndexState					: DINT;
	iInfeedState				: DINT;
	iOutfeedState				: DINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[FOR i := 0 TO 9 DO
	rand[i](Seed:=MAIN.Seed+i);
END_FOR

fbInfeedActuator();
fbInfeedConveyor();
fbIndexConveyor();
fbOutfeedActuator();
fbOutfeedConveyor();

fbSimScanner();
fbSimFiller();
fbSimCapper();
fbSimWeigher();

// Infeed
CASE iInfeedState OF
	0:
		fbTmr[0].PT := TO_TIME(1000 + rand[0].Num * 1000);
		iInfeedState := 1;
		
	1:
		fbTmr[0](IN:=(fbInfeedConveyor.State <> 0 AND NOT bPartPresentAtInfeed));
		IF fbTmr[0].Q THEN
			fbTmr[0](IN:=FALSE);
			bPartPresentAtInfeed := TRUE;
			iInfeedState := 2;
		END_IF
		
	2:
		IF fbInfeedActuator.bExtended THEN
			bPartPresentAtInfeed := FALSE;
			fbSimScanner.PartPresent := TRUE;
			bStationLoaded[0] := TRUE;
			iInfeedState := 3;
		END_IF
	
	3:
		iInfeedState := 0;
		
END_CASE


CASE iIndexState OF
	0:
		// conveyor indexing
		IF fbIndexConveyor.bMoveAbsCmd AND 
			(fbIndexConveyor.fActPosition <> fbIndexConveyor.fPositionCmd) THEN
			iIndexState := 1;
		END_IF
		
	1:
		fbSimScanner.PartPresent := FALSE;
		fbSimFiller.PartPresent := FALSE;
		fbSimCapper.PartPresent := FALSE;
		fbSimWeigher.PartPresent := FALSE;
		
		// index complete
		IF fbIndexConveyor.fActPosition = fbIndexConveyor.fPositionCmd THEN
			iIndexState := 2;
		END_IF
		
	2:
		// shift parts
		fbSimScanner.PartPresent := FALSE;
		fbSimFiller.PartPresent := bStationLoaded[0];
		fbSimCapper.PartPresent := bStationLoaded[1];
		fbSimWeigher.PartPresent := bStationLoaded[2];
		bPartPresentAtOutfeed := bStationLoaded[3];
		
		bStationLoaded[3] := bStationLoaded[2];
		bStationLoaded[2] := bStationLoaded[1];
		bStationLoaded[1] := bStationLoaded[0];
		bStationLoaded[0] := FALSE;
		
		iIndexState := 0;
		
END_CASE

// Outfeed
CASE iOutfeedState OF
	0:
		IF bPartPresentAtOutfeed THEN
			iOutFeedState := 1;
		END_IF
		
	1:
		fbTmr[1](IN:=TRUE, PT:=T#3S);
		IF fbTmr[1].Q THEN
			bPartPresentAtOutfeed := FALSE;
			iOutfeedState := 0;
			fbTmr[1](IN:=FALSE);
		END_IF
		
		IF fbOutfeedActuator.bExtended THEN
			bPartPresentAtOutfeed := FALSE;
			iOutfeedState := 0;
		END_IF
		
END_CASE
]]></ST>
    </Implementation>
    <LineIds Name="FB_SimCell">
      <LineId Id="41" Count="0" />
      <LineId Id="44" Count="1" />
      <LineId Id="42" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="179" Count="1" />
      <LineId Id="33" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="49" Count="2" />
      <LineId Id="56" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="173" Count="3" />
      <LineId Id="118" Count="0" />
      <LineId Id="158" Count="1" />
      <LineId Id="178" Count="0" />
      <LineId Id="160" Count="0" />
      <LineId Id="162" Count="0" />
      <LineId Id="161" Count="0" />
      <LineId Id="163" Count="3" />
      <LineId Id="168" Count="1" />
      <LineId Id="177" Count="0" />
      <LineId Id="167" Count="0" />
      <LineId Id="170" Count="2" />
      <LineId Id="120" Count="0" />
      <LineId Id="119" Count="0" />
      <LineId Id="72" Count="1" />
      <LineId Id="60" Count="0" />
      <LineId Id="69" Count="0" />
      <LineId Id="89" Count="0" />
      <LineId Id="75" Count="2" />
      <LineId Id="74" Count="0" />
      <LineId Id="79" Count="6" />
      <LineId Id="90" Count="0" />
      <LineId Id="86" Count="2" />
      <LineId Id="91" Count="1" />
      <LineId Id="103" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="99" Count="2" />
      <LineId Id="209" Count="0" />
      <LineId Id="102" Count="0" />
      <LineId Id="104" Count="3" />
      <LineId Id="112" Count="1" />
      <LineId Id="71" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="211" Count="4" />
      <LineId Id="238" Count="1" />
      <LineId Id="217" Count="1" />
      <LineId Id="260" Count="4" />
      <LineId Id="252" Count="0" />
      <LineId Id="244" Count="0" />
      <LineId Id="224" Count="0" />
      <LineId Id="240" Count="0" />
      <LineId Id="242" Count="0" />
      <LineId Id="241" Count="0" />
      <LineId Id="236" Count="0" />
      <LineId Id="210" Count="0" />
      <LineId Id="59" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>